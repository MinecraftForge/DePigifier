buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.minecraftforge.net/' }
    }
    dependencies {
        classpath  group: 'net.minecraftforge.gradleutils', name: 'GradleUtils', version: '2.+'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}
apply plugin: 'net.minecraftforge.gradleutils'

group 'net.minecraftforge'

version = gradleutils.getTagOffsetVersion()
logger.lifecycle('Version: ' + version)

ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
    MAIN_CLASS = 'net.minecraftforge.depigifier.Unpig'
    MANIFEST = manifest{
        attributes('Main-Class': MAIN_CLASS)
        attributes([
            'Specification-Title':      'DePigifier',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    project.version,
            'Implementation-Title':     'DePigifier',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/depigifier/')
    }
    MIN_CHANGELOG_TAG = '1.0'
}

repositories {
    mavenLocal()
    maven { url = 'https://maven.minecraftforge.net' }
    mavenCentral()
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
}

dependencies {
    testImplementation('org.junit.jupiter:junit-jupiter:5.5.+')
    testImplementation('org.powermock:powermock-core:2.0+')
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4')
    implementation('com.machinezoo.noexception:noexception:1.3.4')
    implementation('org.slf4j:slf4j-api:1.7.30')
    implementation('org.slf4j:slf4j-simple:1.7.30')
    implementation('org.ow2.asm:asm:9.1')
    implementation('net.minecraftforge:srgutils:0.4.3')
}

shadowJar {
    classifier 'fatjar'
    manifest.from(MANIFEST)
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java

        pom {
            name = 'Depigifier'
            description = 'Remaps SRG files based on Proguard data'
            url = 'https://github.com/MinecraftForge/Depigifier'
            scm {
                url = 'https://github.com/MinecraftForge/Depigifier'
                connection = 'scm:git:git://github.com/MinecraftForge/Depigifier.git'
                developerConnection = 'scm:git:git@github.com:MinecraftForge/Depigifier.git'
            }
            issueManagement {
                system = 'github'
                url = 'https://github.com/MinecraftForge/Depigifier/issues'
            }

            licenses {
                license {
                    name = 'LGPLv2.1'
                    url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                }
            }

            developers {
                developer {
                    id = 'cpw'
                    name = 'cpw'
                }
                developer {
                    id = 'LexManos'
                    name = 'LexManos'
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

changelog {
    fromTag MIN_CHANGELOG_TAG
}